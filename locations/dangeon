# dangeon
$dundincif = {
	cls
	gs'stat'

	'8809 = 6'
	'7111 = 0'
	'2172 = 0'
	'6666 = 4'
	'1111 = 0'
	'3213 = 0'
	'7662 = 2'
	'9312 = 1'
	'0000 = 4'
	'2222 = 0'
	'3333 = 0'
	'5555 = 0'
	'8193 = 3'
	'8096 = 5'
	'7777 = 0'
	'9999 = 4'
	'7756 = 1'
	'6855 = 3'
	'9881 = 5'
	'5531 = 0'
	'There is a clever sense in such tomfoolery, because often clever fool walks.'

	act 'Move away from the signs':gt $curloc
}

$dundinboch = {
	cls
	if openstateDM2 = 0:openstateDM2 = 1
	gs'stat'

	'Soon you will hear the chimes, reconcile those who quarrel.'

	act 'Move away from the barrel':gt $curloc
}

$dundinsvitok = {
	cls
	svitokRTS = 1
	heart += 1
	gs'stat'

	'You picked up the scroll and he crumbled. You like the wind breathed. You listened to the feelings and understand that you have something changed. You feel like where-then, as if in a different dimension beats another heart belongs to you, to die for you in battle.'

	act 'Move away':gt $curloc
}

gs'stat'

if dungeonMark = 0:
	'<center><img src="images/picPRE/dunPRE.jpg"></center>'
	'Man-made caves which can be seen in the depths of the door.'

	act 'Get out of the dungeon':minut += 5 & gt'obekt2'
	act 'Sign on the door':minut += 5 & dungeonMark = 1 & gt $curloc
elseif dungeonMark = 1:
	'<center><img src="images/picPRE/dunPRE1.jpg"></center>'
	'Pretty big and deserted room in which there is nothing but a bunch of bones in a corner.'

	act 'Logout':minut += 5 & dungeonMark = 0 & gt $curloc
	act 'Going deep into the':minut += 5 & dungeonMark = 2 & gt $curloc
elseif dungeonMark = 2:
	if svitokRTS = 1:
		exit
	end

	'<center><img src="images/picPRE/dunPRE2.jpg"></center>'
	'Gallery dilapidated in the recesses of which are visible doors to other rooms.'

	act 'Exit Gallery':minut += 5 & dungeonMark = 1 & gt $curloc
	act 'Enter 1th room':minut += 5 & dungeonMark = 3 & gt $curloc
	act 'Enter 2th room':
		cls

		if openstateDM = 1:
			minut += 5
			dungeonMark = 4
			gt $curloc
		end
		if openstateDM = 0:
			:markanotherrand
			DMN[1] = RAND(0,9)
			DMN[2] = RAND(0,9)
			DMN[3] = RAND(0,9)
			DMN[4] = RAND(0,9)
			if DMN[1] = DMN[2] and DMN[1] = DMN[3] and DMN[1] = DMN[4]:jump'markanotherrand'
			if DMN[1] = 8 and DMN[2] = 8 and DMN[3] = 0 and DMN[4] = 9:jump'markanotherrand'
			if DMN[1] = 7 and DMN[2] = 1 and DMN[3] = 1 and DMN[4] = 1:jump'markanotherrand'
			if DMN[1] = 2 and DMN[2] = 1 and DMN[3] = 7 and DMN[4] = 2:jump'markanotherrand'
			if DMN[1] = 3 and DMN[2] = 2 and DMN[3] = 1 and DMN[4] = 3:jump'markanotherrand'
			if DMN[1] = 7 and DMN[2] = 6 and DMN[3] = 6 and DMN[4] = 2:jump'markanotherrand'
			if DMN[1] = 9 and DMN[2] = 3 and DMN[3] = 1 and DMN[4] = 2:jump'markanotherrand'
			if DMN[1] = 8 and DMN[2] = 1 and DMN[3] = 9 and DMN[4] = 3:jump'markanotherrand'
			if DMN[1] = 8 and DMN[2] = 0 and DMN[3] = 9 and DMN[4] = 6:jump'markanotherrand'
			if DMN[1] = 7 and DMN[2] = 7 and DMN[3] = 5 and DMN[4] = 6:jump'markanotherrand'
			if DMN[1] = 6 and DMN[2] = 8 and DMN[3] = 5 and DMN[4] = 5:jump'markanotherrand'
			if DMN[1] = 9 and DMN[2] = 8 and DMN[3] = 8 and DMN[4] = 1:jump'markanotherrand'
			if DMN[1] = 5 and DMN[2] = 5 and DMN[3] = 3 and DMN[4] = 1:jump'markanotherrand'
			i = 0
			numdmr = 0
			:markdnm
			i += 1

			if DMN[i] = 0 or DMN[i] = 6 or DMN[i] = 9:
				numdmr += 1
			elseif DMN[i] = 8:
				numdmr += 2
			end

			if i < 4:jump'markdnm'

			numberOSDM = input("<<DMN[1]>><<DMN[2]>><<DMN[3]>><<DMN[4]>>=?")
			if numberOSDM < 0 or numberOSDM > 8 or numdmr ! numberOSDM:
				MSG("Silence, nothing changed")
				gt $curloc
			else
				openstateDM = 1
				MSG("The door that something clicked")
				gt $curloc
			end
		end
	end

	if openstateDM2 = 2:act 'Enter 3th room':minut += 5 & dungeonMark = 5 & gt $curloc
elseif dungeonMark = 3:
	'<center><img src="images/picPRE/dunPRE3.jpg"></center>'
	'The room in which the center is a stone dais on which stand various dusty bottles.'
	'On the wall of what is scrawled <a href="exec:dynamic $dundincif ">insignia</a>.'

	act 'Exit the room':minut += 5 & dungeonMark = 2 & gt $curloc
elseif dungeonMark = 4:
	'<center><img src="images/picPRE/dunPRE4.jpg"></center>'
	'Small room in which there are some that <a href="exec:dynamic $dundinboch ">barrel</a>.'

	act 'Exit the room':minut += 5 & dungeonMark = 2 & gt $curloc

	if openstateDM2 = 1:
		'In the other barrel you see a device where you can enter numbers.'
		act 'Enter digits':
			cls

			numberOSDM2 = input("12,1,1,1,2,1,3,?,?,?,?")
			if numberOSDM2 = rikudootvet:
				openstateDM2 = 2
				MSG("Outside, something rumbled.")
			else
				MSG("Silence, nothing changed")
			end
			gt $curloc
		end
	end
elseif dungeonMark = 5:
	'<center><img src="images/picPRE/dunPRE5.jpg"></center>'
	'The room in which the middle, He is sitting on a stone throne skeleton covered with cobwebs.'

	if svitokRTS = 0:'In the hands of a skeleton lying <a href="exec:dynamic $dundinsvitok ">scroll</a>.'

	act 'Exit the room':minut += 5 & dungeonMark = 2 & gt $curloc
end
--- dangeon ---------------------------------

