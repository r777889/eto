# fight
if $ARGS[0] = 'start':
	cla
	inFight = 1
	FColor = RGB(0,0,0)
	BColor = RGB(255,255,255)
	LColor = RGB(106,90,205)
	gs'defence'
	gs'statBat'

	if SasoriBody > 2 and healthV <= 0 or SasoriBody > 2 and mannaV <= 0 or SasoriBody > 2 and willpowerV <= 0:
		SasoriBody = SasoriBody - 1
		gs'priziv','sasori'

		'<b><font color = red>Doll parts scattered on and immediately entered into battle another puppet Sasori.</font></b>'
		'<b><font color = red>You`ve heard the sonorous voice Sasori "I have a lot of dolls, you can not win."</font></b>'
	elseif SasoriBody = 2 and healthV <= 0 or SasoriBody = 2 and mannaV <= 0 or SasoriBody = 2 and willpowerV <= 0:
		SasoriBody = SasoriBody - 1
		gs'priziv','sasori1'

		'<b><font color = red>Last Doll crumbled into pieces and he came to fight Sasori.</font></b>'
	end

	if stunV > 0 and SasoriBody > 1:
		stunV = 0

		'<b><font color = red>Doll began to move again.</font></b>'
	end

	if stunV > 0 and SasoriBody = 1:
		stunV = 0

		'<b><font color = red>Sasori move again.</font></b>'
	end

	if GedoMazo = 1 and healthV <= 0 or GedoMazo = 1 and mannaV <= 0 or GedoMazo = 1 and willpowerV <= 0:
		GedoMazo = 2
		barierV = 0

		'<b><font color = green><<$nameV>> He fell to the ground turning to dust</font></b>'

		gs'priziv','tobi'

		'<b><font color = green><<$nameV>> to attack</font></b>'
	end

	if health <= 0 and heart = 0 and izanagi = 0:
		'<b><font color = red>You lost</font></b>'

		xgt'ender','loss'
		exit
	elseif healthV <= 0 and heartV = 0 and izanagiV = 0:
		'<b><font color = green><<$nameV>> lost</font></b>'

		xgt'ender','win'
		exit
	elseif manna <= 0 and heart = 0 and izanagi = 0:
		'<b><font color = red>You lost</font></b>'

		xgt'ender','loss'
		exit
	elseif mannaV <= 0 and heartV = 0 and izanagiV = 0:
		'<b><font color = green><<$nameV>> lost</font></b>'

		xgt'ender','win'
		exit
	elseif willpower <= 0 and heart = 0 and izanagi = 0:
		'<b><font color = red>You lost</font></b>'

		xgt'ender','loss'
		exit
	elseif willpowerV <= 0 and heartV = 0 and izanagiV = 0:
		'<b><font color = green><<$nameV>> lost</font></b>'

		xgt'ender','win'
		exit
	end

	if izanagiV > 0 and healthV <= 0 or izanagiV > 0 and mannaV <= 0 or izanagiV > 0 and willpowerV <= 0:
		izanagiV = izanagiV - 1

		'<b><font color = red><<$nameV>> simply disappeared</font></b>'

		healthV = vitalV*10 + strenV*5
		mannaV = intelV*magikV + magikV*100 + vitalV*10
		willpowerV = willV*10
		stunV = 0
		if program = 6 and izanagiOnce = 0:izanagiOnce = 1 & unmaterialV = 40 & tobizanagi = 0

		'<b><font color = red><<$nameV>> appeared in front of you is completely healthy. He folded his arms and magic sign said Izanagi - perfect illusion.</font></b>'
		'<b><font color = red>One of his eyes was closed forever, I am paying for this spell.</font></b>'
	elseif heartV > 0 and healthV <= 0 or heartV > 0 and mannaV <= 0 or heartV > 0 and willpowerV <= 0:
		heartV = heartV - 1

		'<b><font color = red><<$nameV>> He staggered, and his heart burst</font></b>'

		healthV = vitalV*10 + strenV*5
		mannaV = intelV*magikV + magikV*100 + vitalV*10
		willpowerV = willV*10
		stunV = 0

		'<b><font color = red><<$nameV>> He stood up and rushed into the fray. He has run another heart.</font></b>'
	elseif izanagi > 0 and health <= 0 or izanagi > 0 and manna <= 0 or izanagi > 0 and willpower <= 0:
		izanagi = izanagi - 1

		'<b><font color = green>You are gone</font></b>'

		health = vital*10 + stren*5
		willpower = intel*5 + will*5
		manna = (intel*magik) + magik*100 + vital*10 + rikudo
		stun = 0
		zerkaloV = 0
		poisonV = 0
		bloodV = 0

		'<b><font color = green>And then he appeared in front of the enemy completely healthy.</font></b>'
	elseif heart > 0 and health <= 0 or heart > 0 and manna <= 0 or heart > 0 and willpower <= 0:
		heart = heart - 1
		zerkaloV = 0
		poisonV = 0
		bloodV = 0

		'<b><font color = green>Your heart could not resist and snapped.</font></b>'

		health = vital*10 + stren*5
		willpower = intel*5 + will*5
		manna = (intel*magik) + magik*100 + vital*10 + rikudo
		stun = 0

		'<b><font color = green>You are not dead, You run another heart.</font></b>'
	end

	gs'before'
	'<b>= = = = = = = = = = = = = (ROUND <<round>>) = = = = = = = = = = = = =</b>'
	'<b><<$nameV>></b> life <b><font color = red><<healthV>></font></b>, manna <b><font color = blue><<mannaV>></font></b>, willpower <b><font color = green><<willpowerV>></font></b>'
	if defenceV > 0:'<b><font color = red>Protection <<defenceV>> units</font></b>'
	if defenceMV > 0:'<b><font color = red>Protection of manna <<defenceMV>> units</font></b>'
	if defenceWV > 0:'<b><font color = red>Protection will <<defenceWV>> units</font></b>'
	'Your life <b><font color = red><<health>></font></b>, manna <b><font color = blue><<manna>></font></b>, willpower <b><font color = green><<willpower>></font></b>'
	if defence > 0:'<b><font color = green>Protection <<defence>> units</font></b>'
	if defenceM > 0:'<b><font color = green>Protection from impacts on manna <<defenceM>> units</font></b>'
	if defenceW > 0:'<b><font color = green>Protect from the effects of the will <<defenceW>> units</font></b>'
	if defenceAct > 0:'<b><font color = green><<defenceAct>> moves regeneration protection</font></b>'
	if defenceActM > 0:'<b><font color = green><<defenceActM>> stroke recovery protection from the effects of manna</font></b>'
	if unmaterial > 0:'<b><font color = green>You`re <<unmaterial>> strokes are not material.</font></b>'
	'<b>= = = = = = = = = = = = = = = = = = = = = = = = = = = = = = =</b>'
	gs'init'

	if hod = 0:
		!player attacks
		'<font color = green>You can attack</font>'

		if gun > 0:
			!Boomstick
			if bulletIn > 0:
				'<<bulletIn>> Charged cartridges in the gun'

				act 'Pistol shot':
					cla
					bulletIn -= 1
					gunpower = 150
					buu = 1
					gunpricel = 1
					Gcrime += 1
					gt'strelba'
				end
			elseif bulletIn <= 0:
				if bullet = 0:
					'Ammunition for the gun ran out.'
				elseif bullet>0:
					'<<bullet>> ammunition for pistols'

					act 'Charge':
						cla
						if bullet < 7:
							bulletIn = bullet
							bullet = 0

							'You charge all of the remaining cartridges in the gun.'
						end
						if bullet >= 7:
							bullet -= 7
							bulletIn = 7

							'You loaded 7 bullets in the gun.'
						end

						act 'Further':gt'fight','sta'
					end
				end
			end
		end

		if Mgun > 0:
			!Boomstick
			if mbulletIn > 0:
				'<<mbulletIn>> Charged cartridges in the machine'

				act 'A single from the machine':
					cla
					mbulletIn -= 1
					buu = 1
					gunpower = 300
					gunpricel = 2
					Gcrime += 1
					gt'strelba'
				end

				if mbulletIn >= 3:
					act 'A short burst of machine':
						cla
						mbulletIn -= 3
						buu = 3
						gunpower = 300
						gunpricel = 1
						Gcrime += buu
						gt'strelba'
					end
					act 'A long line of machine':
						cla
						buu = mbulletIn
						mbulletIn = 0
						gunpower = 300
						gunpricel = 0
						Gcrime += buu
						gt'strelba'
					end
				end
			elseif mbulletIn <= 0:
				if mbullet = 0:
					'Chucks for machine ran out.'
				elseif mbullet > 0:
					'<<mbullet>> ammunition for the machine'

					act 'Charge':
						cla
						if mbullet < 7:
							mbulletIn = mbullet
							mbullet = 0

							'You charge all remaining ammunition into the machine.'
						end
						if mbullet >= 30:
							mbullet -= 30
							mbulletIn = 30

							'You loaded 30 cartridges in the machine.'
						end

						act 'Further':gt'fight','sta'
					end
				end
			end
		end

		if tragun > 0:
			!travmatik
			if tbulletIn > 0:
				'Travmatiki charged'

				act 'Pistol shot':
					cla
					tbulletIn -= 1
					gunpower = 50
					buu = 1
					gunpricel = 0
					gt'strelba'
				end
			elseif tbulletIn <= 0:
				if tbullet = 0:
					'Cartridges for travmatika ended.'
				elseif tbullet > 0:
					'<<tbullet>> cartridges for travmatika'

					act 'Charge':
						cla
						if tbullet >= 1:
							tbullet -= 1
							tbulletIn = 1

							'You loaded a cartridge in travmatik.'
						end

						act 'Further':gt'fight','sta'
					end
				end
			end
		end

		act 'Physical Attack':xgt'boxing'
		if magik > 0:
			act 'Magic Attack':gt'magik'
		end
	elseif hod = 1:
		!enemy attacks
		'<font color = red><<$nameV>> attacks</font>'

		act 'Further':xgt'enemy','start'
	end
end

if $ARGS[0] = 'sta':
	cla
	damTip = 0
	damTipM = 0
	damTipV = 0
	damTipMV = 0
	magweapbonus = 0
	magweapbonusV = 0
	bonusSh = 0
	bonusShV = 0
	eleSh = 0
	eleShV = 0
	poisonAV = 0
	hidanAtk = 0

	'<b>= = = = = = = = = = = = = = = = = = = = = = = = = = = = = = =</b>'
	'<b><<$nameV>></b> life <b><font color = red><<healthV>></font></b>, manna <b><font color = blue><<mannaV>></font></b>, willpower <b><font color = green><<willpowerV>></font></b>'
	if defenceV > 0:'<b><font color = red>Protection <<defenceV>> units</font></b>'
	if defenceMV > 0:'<b><font color = red>Protection of manna <<defenceMV>> units</font></b>'
	if defenceWV > 0:'<b><font color = red>Protection will <<defenceWV>> units</font></b>'
	'Your life <b><font color = red><<health>></font></b>, manna <b><font color = blue><<manna>></font></b>, willpower <b><font color = green><<willpower>></font></b>'
	if defence > 0:'<b><font color = green>Protection <<defence>> units</font></b>'
	if defenceM > 0:'<b><font color = green>Protection from impacts on manna <<defenceM>> units</font></b>'
	if defenceW > 0:'<b><font color = green>Protect from the effects of the will <<defenceW>> units</font></b>'
	if defenceAct > 0:'<b><font color = green><<defenceAct>> moves regeneration protection</font></b>'
	if defenceActM > 0:'<b><font color = green><<defenceActM>> stroke recovery protection from the effects of manna</font></b>'
	'<b>= = = = = = = = = = = = = = = = = = = = = = = = = = = = = = =</b>'

	act 'Further':gt'fight','start'
end
--- fight ---------------------------------

